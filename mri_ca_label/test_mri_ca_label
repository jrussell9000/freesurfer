#!/bin/tcsh -f

#
# test_mri_ca_label
#
# execute mri_ca_label and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: zkaufman $
#    $Date: 2016/07/28 14:17:45 $
#    $Revision: 1.6 $
#
# Copyright Â© 2014 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mri_ca_label,v 1.6 2016/07/28 14:17:45 zkaufman Exp $'

umask 002

# backdoor bypass:
if ( $?SKIP_MRI_CA_LABEL_TEST ) then
  echo "skipping test_mri_ca_label"
  exit 77
endif

#
# extract testing data
#

gunzip -c testdata.tar.gz | tar xvf -

#
# run mri_ca_label using typical input file formats,
# first with one thread then with 8 threads.
#

cd testdata

set log = (../test_mri_ca_label.log)
set threads=( 8 )
if ($?SKIP_MRI_CALABEL_CPU_TEST) then
  echo "skipping test_mri_ca_label"
  exit 77
endif
#goto compare

if (-e $log) rm -f $log 

foreach num ($threads)

    setenv OMP_NUM_THREADS $num
    set cmd=(../mri_ca_label \
        -relabel_unlikely 9 .3 \
        -prior 0.5 \
        -align \
        norm.mgz \
        talairach.m3z \
        ../../distribution/average/RB_all_2016-05-10.vc700.gca \
        aseg.auto_noCCseg.${num}cpu.mgz)
    echo "\n\n $cmd \n\n" |& tee -a $log
    $cmd |& tee -a $log
    if ($status != 0) then
        echo "mri_ca_label FAILED" |& tee -a $log
    exit 1
    endif

end

echo "" |& tee -a $log
echo "" |& tee -a $log
echo "" |& tee -a $log

#
# compare expected results with actual (produced) results
#
compare:
set REF_FILE=(aseg.auto_noCCseg.mgz)
if ("`uname -s`" == "Darwin") then 
  if ("`uname -r`" =~ "11."*) then
    set REF_FILE=(aseg.auto_noCCseg.macos.lion.mgz)
  else if ("`uname -r`" =~ "13."*) then
    set REF_FILE=(aseg.auto_noCCseg.macos.mavericks.mgz)
  else if ("`uname -r`" =~ "15."*) then
    set REF_FILE=(aseg.auto_noCCseg.macos.el_cap.mgz)
  endif
endif

foreach num ($threads)

  set cmd=(../../mri_diff/mri_diff \
    ${REF_FILE} aseg.auto_noCCseg.${num}cpu.mgz);
  echo "\n\n $cmd \n\n" |& tee -a $log
  $cmd |& tee -a $log
  if ($status != 0) then
    echo "$cmd FAILED" |& tee -a $log
    exit 1
  endif

end

#
# cleanup
#
cd ..
rm -Rf testdata

echo "" |& tee -a $log
echo "test_mri_ca_label passed all tests" |& tee -a $log
exit 0
