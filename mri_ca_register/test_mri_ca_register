#!/bin/tcsh -f

#
# test_mri_ca_register
#
# execute mri_ca_register and check results against expected results
#

set VERSION='$Id: test_mri_ca_register,v 1.7 2016/07/28 14:17:45 zkaufman Exp $'

umask 002

if ( $?SKIP_MRI_CA_REGISTER_TEST ) then
  exit 0
endif

#
# extract testing data
#
gunzip -c testdata.tar.gz | tar xvf -

#
# run mri_ca_register using typical input file fommats
#

cd testdata

setenv FREESURFER_HOME ../../distribution
setenv SUBJECTS_DIR ""

setenv OMP_NUM_THREADS 8

# modified (shortened) usage in recon-all: 
set cmd=(../mri_ca_register \
    -nobigventricles \
    -T talairach.lta \
    -align-after \
    -levels 3 \
    -n 2 \
    -tol 1.0 \
    -mask brainmask.mgz \
    norm.mgz \
    $FREESURFER_HOME/average/RB_all_2016-05-10.vc700.gca \
    talairach.m3z)

echo ""
echo $cmd
$cmd
if ($status != 0) then
  echo "mri_ca_register FAILED"
  exit 1
endif

#
# compare expected results with actual (produced) results
#

set REF_VOL = ref_talairach.m3z
if ("`uname -s`" == "Darwin") then
  set REF_VOL = ref_talairach_osx.m3z
  if ("`uname -r`" =~ "15."*) then
    set REF_FILE=(ref_talairach_osx.el_cap.mgz)
  endif
endif
set TST_VOL = talairach.m3z
set cmd=(../../mri_diff/mri_diff --debug \
            $REF_VOL $TST_VOL );
echo ""
echo $cmd
$cmd
set diff_status=$status
if ($diff_status != 0) then
  echo "$cmd FAILED (exit status=$diff_status)"
  exit 1
endif

echo ""
echo ""
echo ""

#
# cleanup
#
cd ..
rm -Rf testdata

echo ""
echo "test_mri_ca_register passed all tests"
exit 0
